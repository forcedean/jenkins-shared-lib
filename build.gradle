plugins {
    id "groovy"
}

group = "com.icheko.jenkins.lib"
version = "1.0.0"

repositories {
    mavenCentral()
    maven {
        url "https://repo.jenkins-ci.org/releases/"
    }
    maven {
        url "https://nexus.pentaho.org/repository/proxy-public-3rd-party-release/"
    }
}

dependencies {
    implementation 'org.codehaus.groovy:groovy-all:2.5.0'
    testImplementation 'com.lesfurets:jenkins-pipeline-unit:1.1'
    testImplementation 'junit:junit:4.12'
    testImplementation 'io.fabric8:kubernetes-client:5.6.0'
    testImplementation 'org.csanchez.jenkins.plugins:kubernetes:1.30.1'
    testImplementation 'org.connectbot.jbcrypt:jbcrypt:1.0.0'
}

test {
    // delete old test reports
    dependsOn cleanTest

    // don't stop if tests fail
    ignoreFailures = true

    // minimize logging
    testLogging.maxGranularity = 0

    // show stdout from tests
    // onOutput { dest, event -> print event.message }

    // show test results
    def results = []
    afterTest { desc, result -> println "${desc.className.split("\\.")[-1]}: " + 
        "${desc.name}: ${result.resultType}" }
    afterSuite { desc, result -> if (desc.className) { results << result } }

    // show summary
    doLast {
        println ""
        println "Tests: ${results.sum { it.testCount }}" +
            ", Failures: ${results.sum { it.failedTestCount }}" +
            ", Errors: ${results.sum { it.exceptions.size() }}" + 
            ", Skipped: ${results.sum { it.skippedTestCount }}" 
    }
}